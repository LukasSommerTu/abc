aspect CJPPointcutExpr{

	inh lazy AdviceSpec CJPPointcutExpr.getAdviceSpec();	
	eq Program.getCompilationUnit().getAdviceSpec(){ throw new InternalCompilerError("Trying to get an AdviceSpec");}
	eq AdviceDecl.getPointcutExpr().getAdviceSpec() = getAdviceSpec();
	eq CJPAdviceDecl.getPointcutExpr().getAdviceSpec() = getAdviceSpec();
	
	
	inh lazy Access CJPPointcutExpr.getJPIAccess();
	eq CJPAdviceDecl.getPointcutExpr().getJPIAccess() = getName();

    inh lazy AdviceDecl CJPPointcutExpr.getCurrentAdvice();
    eq Program.getCompilationUnit().getCurrentAdvice(){ throw new InternalCompilerError("Trying to get an advice decl");}
    eq AdviceDecl.getPointcutExpr().getCurrentAdvice() = this;
    
    inh lazy Collection<CJPAdviceDecl> CJPPointcutExpr.getCJPAdviceDecls();
	eq Program.getCompilationUnit().getCJPAdviceDecls(){throw new InternalCompilerError("Trying to get CJPAdviceDecl");}
    eq AspectDecl.getBodyDecl().getCJPAdviceDecls(){
	  	HashSet<CJPAdviceDecl> set = new HashSet<CJPAdviceDecl>();
    	for(BodyDecl bd : getBodyDecls()){
    		if(bd.isCJPAdviceDecl()){
    			set.add((CJPAdviceDecl)bd);
    		}
    	}
    	return set;
    }	    
}