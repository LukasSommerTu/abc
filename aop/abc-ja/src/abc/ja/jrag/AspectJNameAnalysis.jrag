aspect AspectJNameAnalysis {

  // transitive "implements" test
  syn boolean TypeDecl.implement(TypeDecl typeDecl) = false;
  eq ClassDecl.implement(TypeDecl typeDecl) {
    for(int i = 0; i < getNumImplements(); i++)
      if(getImplements(i).type().instanceOf(typeDecl))
        return true;
    return false;
  }

  refine IntertypeMethodErrorCheck eq AspectDecl.localMethodsSignatureMap() {
    addImplicitMethods();
    return IntertypeMethodErrorCheck.AspectDecl.localMethodsSignatureMap();
  }

  private boolean AspectDecl.addImplicitMethods = true;
  private void AspectDecl.addImplicitMethods() {
    if(!addImplicitMethods || isAbstract())
      return;
    addImplicitMethods = false;
    List parameters = new List();
    if(getPerClause().isPerObject())
      parameters.add(new ParameterDeclaration(new Modifiers(new List()), typeObject().createQualifiedAccess(), "obj"));
    MethodDecl aspectOf = 
      new MethodDecl(
        new Modifiers(new List().add(new Modifier("public")).add(new Modifier("static")).add(new Modifier("synthetic"))),
        createQualifiedAccess(),
        "aspectOf",
        parameters,
        new List(),
        new List(),
        new Opt(new Block(new List().add(new ReturnStmt(new Opt(new NullLiteral())))))
      );
    parameters = new List();
    if(getPerClause().isPerObject())
      parameters.add(new ParameterDeclaration(new Modifiers(new List()), typeObject().createQualifiedAccess(), "obj"));
    MethodDecl hasAspect =
      new MethodDecl(
        new Modifiers(new List().add(new Modifier("public")).add(new Modifier("static")).add(new Modifier("synthetic"))),
        typeBoolean().createQualifiedAccess(),
        "hasAspect",
        parameters,
        new List(),
        new List(),
        new Opt(new Block(new List().add(new ReturnStmt(new Opt(new BooleanLiteral("true"))))))
      );
    aspectOf.category = MethodCategory.ASPECT_INSTANCE;
    hasAspect.category = MethodCategory.ASPECT_INSTANCE;
    addBodyDecl(aspectOf);
    addBodyDecl(hasAspect);
  }
  inh TypeDecl AspectDecl.typeBoolean();

  syn boolean PerClause.isPerObject() = false;
  eq PerTarget.isPerObject() = true;
  eq PerThis.isPerObject() = true;

  eq AspectDecl.getPerClause().hostType() = this;

  /* ------------------------------------------------------------------------------------------------------ */
  // named pointcut (sketch)
  // the main difficulty here is to check that there are no cycles
  // in pointcut declarations. In JastAdd, that can be done via
  // circular attributes.
  
  /*
  // quick hack: we only find pcs in the declaring aspect
  inh PCDecl PCName.lookupPC(String name);
  inh PCDecl PCDecl.lookupPC(String name);
  eq Program.getCompilationUnit(int i).lookupPC(String name) = null;
  eq AspectDecl.getBodyDecl(int index).lookupPC(String name) {
    for(int i = 0; i < getNumBodyDecl(); i++)
      if(getBodyDecl(i).isPC(name))
        return (PCDecl)getBodyDecl(i);
    return null;
  }
  syn boolean BodyDecl.isPC(String name) = false;
  eq PCDecl.isPC(String name) = getID().equals(name);
  syn PCDecl PCName.decl() = lookupPC(getID());
  
  // checking the name for circular use
  public void PCDecl.nameCheck() {
    super.nameCheck();
    if(isCircular())
      error("Semantic Error: pointcut " + getID() + " in " + hostType().fullName() + " is circular");
  }

  // do a fixpoint computation over the boolean domain
  syn lazy boolean PCDecl.isCircular() circular [true] = getPC().isCircular();
  syn lazy boolean PC.isCircular() circular [true];
  eq PCAnd.isCircular() = getLeft().isCircular() || getRight().isCircular();
  eq PCCall.isCircular() = false;
  eq PCName.isCircular() = decl().isCircular();
  */

}
